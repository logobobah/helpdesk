
Функция ПолучитьЦенуЗадачи(Задача) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧасовыеСтавкиЗадачСрезПоследних.Период КАК Период,
		|	ЧасовыеСтавкиЗадачСрезПоследних.Заказчик КАК Заказчик,
		|	ЧасовыеСтавкиЗадачСрезПоследних.Проект КАК Проект,
		|	ЧасовыеСтавкиЗадачСрезПоследних.Исполнитель КАК Исполнитель,
		|	ЧасовыеСтавкиЗадачСрезПоследних.Ставка КАК Ставка
		|ПОМЕСТИТЬ втСтавки
		|ИЗ
		|	РегистрСведений.ЧасовыеСтавкиЗадач.СрезПоследних(
		|			&Период,
		|			Заказчик = &Заказчик
		|				И (Проект = &Проект
		|					ИЛИ Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка))
		|				И (Исполнитель = &Исполнитель
		|					ИЛИ Исполнитель = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))) КАК ЧасовыеСтавкиЗадачСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСтавки.Заказчик КАК Заказчик,
		|	втСтавки.Проект КАК Проект,
		|	втСтавки.Исполнитель КАК Исполнитель,
		|	втСтавки.Ставка КАК Ставка,
		|	1 КАК Приоритет
		|ИЗ
		|	втСтавки КАК втСтавки
		|ГДЕ
		|	втСтавки.Проект = &Проект
		|			И втСтавки.Исполнитель = &Исполнитель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втСтавки.Заказчик,
		|	втСтавки.Проект,
		|	втСтавки.Исполнитель,
		|	втСтавки.Ставка,
		|	2
		|ИЗ
		|	втСтавки КАК втСтавки
		|ГДЕ
		|	втСтавки.Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|			И втСтавки.Исполнитель = &Исполнитель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втСтавки.Заказчик,
		|	втСтавки.Проект,
		|	втСтавки.Исполнитель,
		|	втСтавки.Ставка,
		|	3
		|ИЗ
		|	втСтавки КАК втСтавки
		|ГДЕ
		|	втСтавки.Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|			И втСтавки.Исполнитель = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
	Запрос.УстановитьПараметр("Заказчик", Задача.Заказчик);
	Запрос.УстановитьПараметр("Исполнитель", Задача.Исполнитель);
	Запрос.УстановитьПараметр("Период", Задача.Дата);
	Запрос.УстановитьПараметр("Проект", Задача.Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ставка;
	КонецЦикла;

	Возврат 0;
КонецФункции