
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		
			// Ограничение доступа:
			// Редактирование разрешено владельцу календаря 
			// Просмотр разрешен сотруднику, указанному в ТЧ "Доступ"
			
			ПравоРедактирования = Объект.ВладелецКалендаря = ТекущийПользователь;
			ПравоПросмотра = ПравоРедактирования;
			Отбор = Новый Структура("Сотрудник", ТекущийПользователь);
			НайденныеСтроки = Объект.Доступ.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 0 Тогда
				ПравоПросмотра = Истина;
			КонецЕсли;
			
			Если Не ПравоПросмотра Тогда
				ВызватьИсключение НСтр("ru='Недостаточно прав для просмотра календаря.'");
			КонецЕсли;
			
			ТолькоПросмотр = Не ПравоРедактирования;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьВидимостьОтметкиСинхронизироватьСGoogle();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрОповещения = Новый Структура;
	ПараметрОповещения.Вставить("Календарь", Объект.Ссылка);
	ПараметрОповещения.Вставить("Наименование", Объект.Наименование);
	ПараметрОповещения.Вставить("ВладелецКалендаря", Объект.ВладелецКалендаря);
	
	Оповестить("Запись_КалендарьСотрудника", ПараметрОповещения, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВладелецКалендаряПриИзменении(Элемент)
	
	Если ТекущийПользователь = Объект.ВладелецКалендаря Тогда
		Возврат;
	КонецЕсли;
	ЕстьДоступ = Ложь;
	Отбор = Новый Структура("Сотрудник", ТекущийПользователь);
	Если Объект.Доступ.НайтиСтроки(Отбор).Количество() > 0 Тогда
		ЕстьДоступ = Истина;
	КонецЕсли;
	
	Если Не ЕстьДоступ Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавлениеСотрудникаПредложено", ЭтотОбъект);
		
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
		ПараметрыВопроса.Заголовок = НСтр("ru='Изменение доступа'");
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		ТекстВопроса = СтрШаблон(НСтр("ru='Текущий сотрудник %1 не имеет доступа к календарю.
			|Разрешить доступ?'"), Строка(ТекущийПользователь));
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, ПараметрыВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВидимостьОтметкиСинхронизироватьСGoogle()
	
	Если Не ЗначениеЗаполнено(Объект.ВладелецКалендаря) Тогда
		Элементы.СинхронизироватьСGoogle.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ОтключенныеОбластиДоступа = РегистрыСведений.СеансовыеДанныеGoogle.ОтключенныеОбластиДоступа(Объект.ВладелецКалендаря);
	
	Элементы.СинхронизироватьСGoogle.Видимость = ОтключенныеОбластиДоступа.Найти(Перечисления.ОбластиДоступаGoogle.Календарь) = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавлениеСотрудникаПредложено(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВопроса) = Тип("Структура")
		И РезультатВопроса.Свойство("Значение")
		И РезультатВопроса.Значение = КодВозвратаДиалога.Да Тогда
		
		НоваяСтрока = Объект.Доступ.Добавить();
		НоваяСтрока.Сотрудник = ТекущийПользователь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
