#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	ТипОбъекта = ТипЗнч(Объект);
	
	Если Предопределенный Тогда
		ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.Раздел;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Пользователи") Тогда
		ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.Пользователь;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Заказчики") Тогда
		ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.Заказчик;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.КонтактныеЛицаЗаказчиков") Тогда
		ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.КонтактноеЛицо;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ГруппыЛичныхАдресатов") Тогда
		ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.ГруппаЛичныхАдресатов;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ЛичныеАдресаты") Тогда
		ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.ЛичныйАдресат;
	ИначеЕсли ТипОбъекта = Тип("Строка") Тогда
		
		ЭтоАвтогруппа = Ложь;
		ФункцииАвтоГрупп = РаботаСАдреснойКнигой.ПолучитьСписокДоступныхФункций();
		Для Каждого ФункцияАвтогруппа Из ФункцииАвтоГрупп Цикл
			Если ФункцияАвтогруппа.Представление = Объект Тогда
				ЭтоАвтогруппа = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтоАвтогруппа Тогда
			ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.Автогруппа;
		Иначе
			ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.АдресСпискаРассылки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти