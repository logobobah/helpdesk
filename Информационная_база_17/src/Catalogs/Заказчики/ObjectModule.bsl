

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ОбновитьДанныеЗаказчикаВАдреснойКниге;
Перем ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге;
Перем ОбновитьДанныеОтображенияЗаказчикаВАдреснойКниге;
Перем ОбновитьДоступностьВПоискеПоЗаказчику;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	РеквизитыКонтрагентаПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка, "Наименование, ГруппаДоступа, Актуален, ПометкаУдаления, Родитель, КонтактнаяИнформация");
	
	// Обновление адресной книги.
	ОбновитьДанныеЗаказчикаВАдреснойКниге = Ложь;
	ОбновитьДанныеОтображенияЗаказчикаВАдреснойКниге = Ложь;
	ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге = Ложь;
	ОбновитьДоступностьВПоискеПоЗаказчику = Ложь;
	
	Если ЭтоНовый() Тогда
		ОбновитьДанныеЗаказчикаВАдреснойКниге = Истина;
		Если НЕ ЭтоГруппа Тогда
			ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге = Истина;
		КонецЕсли;
	Иначе
		Если РеквизитыКонтрагентаПоСсылке.Родитель <> Родитель Тогда
			ОбновитьДанныеЗаказчикаВАдреснойКниге = Истина;
		КонецЕсли;
		Если РеквизитыКонтрагентаПоСсылке.ПометкаУдаления <> ПометкаУдаления ИЛИ РеквизитыКонтрагентаПоСсылке.Актуален <> Актуален Тогда
			ОбновитьДанныеЗаказчикаВАдреснойКниге = Истина;
			ОбновитьДанныеОтображенияЗаказчикаВАдреснойКниге = Истина;
		КонецЕсли;
		Если РеквизитыКонтрагентаПоСсылке.Наименование <> Наименование Тогда
			ОбновитьДанныеОтображенияЗаказчикаВАдреснойКниге = Истина;
			ОбновитьДанныеЗаказчикаВАдреснойКниге = Истина;
		КонецЕсли;
		
		Если НЕ ЭтоГруппа Тогда
			Если РеквизитыКонтрагентаПоСсылке.Наименование <> Наименование Тогда
				ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге = Истина;
			КонецЕсли;
			Если НЕ ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге Тогда
				СтараяКонтактнаяИнформация = РеквизитыКонтрагентаПоСсылке.
					КонтактнаяИнформация.Выгрузить().ВыгрузитьКолонку("Представление");
				НоваяКонтактнаяИнформация = КонтактнаяИнформация.Выгрузить().ВыгрузитьКолонку("Представление");
				Для Каждого СтрИнфо ИЗ НоваяКонтактнаяИнформация Цикл
					Если СтараяКонтактнаяИнформация.Найти(СтрИнфо) = Неопределено Тогда
						ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если НЕ ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге Тогда
					Для Каждого СтрИнфо ИЗ СтараяКонтактнаяИнформация Цикл
						Если НоваяКонтактнаяИнформация.Найти(СтрИнфо) = Неопределено Тогда
							ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если РеквизитыКонтрагентаПоСсылке.ПометкаУдаления <> ПометкаУдаления Тогда
			ОбновитьДоступностьВПоискеПоЗаказчику = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Обновление адресной книги
	Если ОбновитьДанныеЗаказчикаВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			Ссылка,
			Родитель,
			Справочники.АдреснаяКнига.Заказчики,
			Ссылка);
	КонецЕсли;
	Если ОбновитьДанныеОтображенияЗаказчикаВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОтображенияПодчиненногоОбъекта(Ссылка);
	КонецЕсли;
	Если ОбновитьСловаПоискаПоЗаказчикуВАдреснойКниге Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоКонтрагенту(ЭтотОбъект);
	КонецЕсли;
	Если ОбновитьДоступностьВПоискеПоЗаказчику Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьДоступностьВПоиске(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли