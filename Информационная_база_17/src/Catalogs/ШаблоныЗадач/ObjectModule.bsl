
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Перем пПроект;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Проект", пПроект) И Не ДанныеЗаполнения.Свойство("Заказчик") Тогда
			ДанныеЗаполнения.Вставить("Заказчик", ЗадачиВызовСервера.ПолучитьОсновногоЗаказчикаПроекта(пПроект));
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Заказчик") Тогда
			Заказчик = ДанныеЗаполнения.Заказчик;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Проект") Тогда
			Проект = ДанныеЗаполнения.Проект;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Задачи") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		НаименованиеЗадачи = ДанныеЗаполнения.Наименование;
		Если ДанныеЗаполнения.Срок > ДанныеЗаполнения.Дата Тогда
			СрокВДнях = (ДанныеЗаполнения.Срок - ДанныеЗаполнения.Дата) / 86400;
		КонецЕсли;
		
		//Теги из шаблона
		Для Каждого СтрокаТег Из ДанныеЗаполнения.Теги Цикл
			НовыеДанныеТега = Теги.Добавить();
			НовыеДанныеТега.Тег = СтрокаТег.Тег;
		КонецЦикла;
		
	КонецЕсли;
	Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	Если  ТипЗнч(Пользователь) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
		Заказчик = Пользователь.ОбъектАвторизации.Владелец;
	Иначе
		Исполнитель = Пользователь;
	КонецЕсли;
	Если КалендарьСотрудника.Пустая() ИЛИ КалендарьСотрудника.ВладелецКалендаря <> Исполнитель Тогда
		КалендарьСотрудника = Исполнитель.ОсновнойКалендарь;
	КонецЕсли;
	Статус = Перечисления.СтатусыЗадач.Новая;
	Документы.Задачи.ЗаполнитьМинимальныйПриоритетЗадачи(ЭтотОбъект);
КонецПроцедуры

