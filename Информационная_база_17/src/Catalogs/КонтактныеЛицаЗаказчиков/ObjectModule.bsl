
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьДанныеКонтактногоЛицаВАдреснойКниге;
Перем ОбновитьДанныеОтображенияКонтактногоЛицаВАдреснойКниге;
Перем ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге;
Перем ЗаказчикиДляОбновленияСловПоиска;
Перем ОбновитьДоступностьВПоискеПоКонтактномуЛицу;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	ОбновитьДанныеКонтактногоЛицаВАдреснойКниге = Ложь;
	ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге = Ложь;
	ОбновитьДанныеОтображенияКонтактногоЛицаВАдреснойКниге = Ложь;
	ЗаказчикиДляОбновленияСловПоиска = Новый Массив;
	ОбновитьДоступностьВПоискеПоКонтактномуЛицу = Ложь;
	Если ЭтоНовый() Тогда
		ОбновитьДанныеКонтактногоЛицаВАдреснойКниге = Истина;
		ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге = Истина;
		ЗаказчикиДляОбновленияСловПоиска.Добавить(Владелец);
	Иначе
		
		РеквизитыКонтактногоЛицаПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Ссылка, "Наименование, Владелец, Актуален, ПометкаУдаления, КонтактнаяИнформация");
		
		Если РеквизитыКонтактногоЛицаПоСсылке.Владелец <> Владелец Тогда
			ОбновитьДанныеКонтактногоЛицаВАдреснойКниге = Истина;
			ЗаказчикиДляОбновленияСловПоиска.Добавить(РеквизитыКонтактногоЛицаПоСсылке.Владелец);
			ЗаказчикиДляОбновленияСловПоиска.Добавить(Владелец);
		КонецЕсли;
		Если РеквизитыКонтактногоЛицаПоСсылке.ПометкаУдаления <> ПометкаУдаления ИЛИ РеквизитыКонтактногоЛицаПоСсылке.Актуален <> Актуален Тогда
			ОбновитьДанныеКонтактногоЛицаВАдреснойКниге = Истина;
			ОбновитьДанныеОтображенияКонтактногоЛицаВАдреснойКниге = Истина;
			ОбновитьДоступностьВПоискеПоКонтактномуЛицу = Истина;
		КонецЕсли;
		
		Если РеквизитыКонтактногоЛицаПоСсылке.Наименование <> Наименование Тогда
			ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге = Истина;
			ОбновитьДанныеОтображенияКонтактногоЛицаВАдреснойКниге = Истина;
			ОбновитьДанныеКонтактногоЛицаВАдреснойКниге = Истина;
		КонецЕсли;
		Если НЕ ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге Тогда
			СтараяКонтактнаяИнформация = РеквизитыКонтактногоЛицаПоСсылке.
				КонтактнаяИнформация.Выгрузить().ВыгрузитьКолонку("Представление");
			НоваяКонтактнаяИнформация = КонтактнаяИнформация.Выгрузить().ВыгрузитьКолонку("Представление");
			Для Каждого СтрИнфо ИЗ НоваяКонтактнаяИнформация Цикл
				Если СтараяКонтактнаяИнформация.Найти(СтрИнфо) = Неопределено Тогда
					ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если НЕ ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге Тогда
				Для Каждого СтрИнфо ИЗ СтараяКонтактнаяИнформация Цикл
					Если НоваяКонтактнаяИнформация.Найти(СтрИнфо) = Неопределено Тогда
						ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	Если ОбновитьДанныеКонтактногоЛицаВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			Ссылка,
			Владелец,
			Справочники.АдреснаяКнига.Заказчики,
			Ссылка);
	КонецЕсли;
	Если ОбновитьДанныеОтображенияКонтактногоЛицаВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОтображенияПодчиненногоОбъекта(Ссылка);
	КонецЕсли;
	Если ОбновитьСловаПоискаПоКонтактномуЛицуВАдреснойКниге Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоКонтактномуЛицу(ЭтотОбъект);
	КонецЕсли;
	Если ЗаказчикиДляОбновленияСловПоиска.Количество() > 0 Тогда
		Для Каждого Контрагент Из ЗаказчикиДляОбновленияСловПоиска Цикл
			РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоКонтрагенту(Контрагент);
		КонецЦикла;
	КонецЕсли;
	Если ОбновитьДоступностьВПоискеПоКонтактномуЛицу Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьДоступностьВПоиске(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли