
&НаКлиенте
Перем ТекущийЭлементФормы;

&НаКлиенте
Процедура ОбработкаОжиданияКонтактныеЛица()
	
	Если Элементы.Заказчики.ТекущаяСтрока <> Неопределено Тогда 
		СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Заказчик", Элементы.Заказчики.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Заказчик", Элементы.Заказчики.ТекущаяСтрока);
	
	ЛичныйАдресат = Параметры.ЛичныйАдресат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапомнитьТекущийЭлемент();
	ПодключитьОбработчикОжидания("ЗапомнитьТекущийЭлемент", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьТекущийЭлемент()
	
	Если ТекущийЭлемент = Элементы.Заказчики 
		Или ТекущийЭлемент = Элементы.СписокКонтактныеЛица Тогда
		ТекущийЭлементФормы = ТекущийЭлемент;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикиПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжиданияКонтактныеЛица", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКонтактныеЛицаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЭтаФорма.ТекущийЭлемент = Элементы.СписокКонтактныеЛица Тогда 
		
		Если Элементы.СписокКонтактныеЛица.ТекущаяСтрока = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
	
		ОповеститьОВыборе(Элементы.СписокКонтактныеЛица.ТекущаяСтрока);
		
	Иначе
		
		Если Элементы.Заказчики.ТекущаяСтрока = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
	
		ОповеститьОВыборе(Элементы.Заказчики.ТекущаяСтрока);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКонтактныеЛицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Заказчики.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;	
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", ТекущиеДанные.Ссылка);

	ПараметрыФормы = Новый Структура("ЛичныйАдресат", ЛичныйАдресат);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	ОткрытьФорму("Справочник.КонтактныеЛицаЗаказчиков.ФормаОбъекта", ПараметрыФормы, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда 
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЛичныйАдресат", ЛичныйАдресат);
		
	ОткрытьФорму("Справочник.Заказчики.ФормаОбъекта", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлемент(Команда)
	
	Если ТекущийЭлементФормы = Неопределено 
		Или ТекущийЭлементФормы = Элементы.Заказчики Тогда
		
		Элементы.Заказчики.ДобавитьСтроку();
		
	ИначеЕсли ТекущийЭлементФормы = Элементы.СписокКонтактныеЛица Тогда
		
		Элементы.СписокКонтактныеЛица.ДобавитьСтроку();
		
	КонецЕсли;
	
КонецПроцедуры

