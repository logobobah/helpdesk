
#Область СлужебныеПроцедурыИФункции

// Функция создания тега
//
// Параметры:
//  НаименованиеТега - строка - имя тега
// Возвращаемое значение:
//  СправочникСсылка.Теги - ссылка на созданный элемент
Функция СоздатьТег(НаименованиеТега) Экспорт
	
	Возврат КлассификацияКонтактов.СоздатьТег(НаименованиеТега);
	
КонецФункции

// Функция получения задач по тегам
//
// Параметры:
//  Теги - массив - СправочникСсылка.Теги - теги по которым необходимо получить контрагентов
// Возвращаемое значение:
//  массив - ДокументСсылка.Задачи - задачи содержащие все заданные теги.
Функция ЗадачиПоТегам(Теги) Экспорт
	
	Задачи = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Задачи.Ссылка КАК Задача,
		|	Задачи.Теги.(
		|		Тег
		|	)
		|ИЗ
		|	Документ.Задачи КАК Задачи";
	
	ВыборкаЗадачи = Запрос.Выполнить().Выбрать();
	Отбор = Новый Структура("Тег");
	
	Пока ВыборкаЗадачи.Следующий() Цикл
		
		ВыборкаТеги = ВыборкаЗадачи.Теги.Выбрать();
		ЕстьВсеТеги = Истина;
		
		Для Каждого Тег Из Теги Цикл
			Отбор.Тег = Тег;
			ВыборкаТеги.Сбросить();
			Если НЕ ВыборкаТеги.НайтиСледующий(Отбор) Тогда
				ЕстьВсеТеги = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьВсеТеги Тогда
			Задачи.Добавить(ВыборкаЗадачи.Задача);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Задачи;
	
КонецФункции

Функция УстановитьТег(ВладельцыТегов, Тег) Экспорт
	
	Возврат КлассификацияКонтактов.УстановитьТег(ВладельцыТегов, Тег);
	
КонецФункции

#КонецОбласти
