
#Область ПрограммныйИнтерфейс

// Возвращает подпись пользователя, используемую для отправки почтовых сообщений.
//
// Возвращаемое значение:
//  Строка - подпись в формате HTML.
//
Функция ПодписьПисьма() Экспорт
	
	ШаблонПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ПодписьДляНовыхПисем");
	Возврат Справочники.ШаблоныТекстов.ПолучитьТекстШаблона(ШаблонПисьма, Документы.ИсходящееПисьмо.ПустаяСсылка());
	
КонецФункции

// Возвращает адреса электронной почты из контактной информации одного или нескольких заказчиков и контактных лиц.
// См. также ОтправкаПочтовыхСообщений.АдресаЭлектроннойПочты()
//
Функция АдресаЭлектроннойПочты(Заказчик) Экспорт
	
	Получатели = Новый Массив;
	
	Запрос = Новый Запрос;
	Если ТипЗнч(Заказчик) = Тип("СправочникСсылка.Заказчики") Тогда
		Запрос.УстановитьПараметр("Заказчики", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Заказчик));
	Иначе
		Запрос.УстановитьПараметр("Заказчики", Заказчик);
	КонецЕсли;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заказчики.Ссылка КАК Заказчик,
	|	Заказчики.ОсновноеКонтактноеЛицо.Наименование КАК Представление,
	|	Заказчики.ОсновноеКонтактноеЛицо КАК КонтактноеЛицо
	|ПОМЕСТИТЬ КонтактныеЛица
	|ИЗ
	|	Справочник.Заказчики КАК Заказчики
	|ГДЕ
	|	Заказчики.Ссылка В(&Заказчики)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КонтактноеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛОЖЬ КАК Выбран,
	|	КонтактныеЛицаКонтактнаяИнформация.Представление КАК Адрес,
	|	КонтактныеЛица.Заказчик КАК ИсточникКонтактнойИнформации,
	|	КонтактныеЛица.Представление КАК Представление,
	|	КонтактныеЛица.КонтактноеЛицо КАК КонтактноеЛицо
	|ИЗ
	|	КонтактныеЛица КАК КонтактныеЛица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаЗаказчиков.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|		ПО КонтактныеЛица.КонтактноеЛицо = КонтактныеЛицаКонтактнаяИнформация.Ссылка
	|			И (КонтактныеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКЛ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсточникКонтактнойИнформации";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыПолучателя = ОтправкаПочтовыхСообщений.НовыеПараметрыПолучателя();
		Если ТипЗнч(Заказчик) = Тип("СправочникСсылка.Заказчики") Тогда
			ПараметрыПолучателя.ИсточникКонтактнойИнформации = Заказчик;
		КонецЕсли;
		Получатели.Добавить(ПараметрыПолучателя);
		Возврат Получатели;
	КонецЕсли;
	
	Контакты = РезультатЗапроса.Выгрузить();
	ОтправкаПочтовыхСообщений.УстановитьПолучателяПоУмолчанию(Контакты);
	Контакты.Колонки.Удалить("КонтактноеЛицо");
	
	Для Каждого Контакт Из Контакты Цикл
		ПараметрыПолучателя = ОтправкаПочтовыхСообщений.НовыеПараметрыПолучателя();
		ЗаполнитьЗначенияСвойств(ПараметрыПолучателя, Контакт);
		// Уберем из представления символы которые не пройдут проверку ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки().
		ПараметрыПолучателя.Представление = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(
			"№", ПараметрыПолучателя.Представление, " ");
		Получатели.Добавить(ПараметрыПолучателя);
	КонецЦикла;
	
	Возврат Получатели;
	
КонецФункции

// Дополняет структуру параметров электронного письма для отправки документа.
//
// Параметры:
//  ПараметрыПисьма - Структура параметров для передачи в функцию РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо.
//  НаборПечатныхФорм - Массив - содержит структуры с табличными документами и объектами печати.
//   см.ДополнительныеОтчетыИОбработки.ПечатьПоВнешнемуИсточнику и УправлениеПечатью.СформироватьПечатныеФормыДляБыстройПечати.
//  МенеджерПечати - Строка - имя менеджера печати.
//  ДополнительныеПараметры - Структура - дополнительные параметры для сохранения печатных форм:
//   * УпаковатьВАрхив - Булево - упаковать печатные формы в архив.
//   * ФорматыСохранения - Массив - содержит формат сохранения файлов см. ТипФайлаТабличногоДокумента.
//
Процедура ПараметрыЭлектронногоПисьма(ПараметрыПисьма, НаборПечатныхФорм, МенеджерПечати, ДополнительныеПараметры = Неопределено) Экспорт
	
	ИсходныеДанные = Новый Структура;
	ИсходныеДанные.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ИсходныеДанные.Вставить("МенеджерПечати",          МенеджерПечати);
	ИсходныеДанные.Вставить("НаборПечатныхФорм",       НаборПечатныхФорм);
	АдресИсходногоВложения = ПоместитьВоВременноеХранилище(ИсходныеДанные, Новый УникальныйИдентификатор);
	
	ПараметрыПисьма.Вставить("АдресИсходногоВложения", АдресИсходногоВложения);
	
КонецПроцедуры

// Дополняет структуру параметров команд формирования печатных форм для отправки электронного документа.
//
// Параметры:
//  КомандыОтправки - ТаблицаЗначений, содержащая подготовленные команды формирования форм для отправки
//   см.ОтправкаПочтовыхСообщений.КомандыОтправки.
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	АдресВременногоХранилищаФаксимиле = ПоместитьВоВременноеХранилище("", Новый УникальныйИдентификатор);
	
	Для Каждого КомандаОтправки Из КомандыОтправки Цикл
		КомандаОтправки.ДополнительныеПараметры.Вставить("АдресВременногоХранилищаФаксимиле", АдресВременногоХранилищаФаксимиле);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
