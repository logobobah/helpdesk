
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КоличествоКартинок = 18;
	
	МассивРеквизитов = Новый Массив;
	ТипРеквизита = Новый ОписаниеТипов("Число",
				   Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный));
	Для Сч = 0 по КоличествоКартинок-1 Цикл
		Реквизит = Новый РеквизитФормы("Маркер_"+Сч, ТипРеквизита);
		МассивРеквизитов.Добавить(Реквизит);
	КонецЦикла;
	ИзменитьРеквизиты(МассивРеквизитов);
	Для Сч = 0 по КоличествоКартинок-1 Цикл
		ЭтаФорма["Маркер_"+Сч] = Сч;
	КонецЦикла;
	
	ВСтроке = Окр(Sqrt(КоличествоКартинок)+0.5);
	СчГрупп = 0; 
	Для Сч = 0 по КоличествоКартинок-1 Цикл
		Если Сч % ВСтроке = 0 Тогда
			ТекГруппа = Элементы.Добавить("Группа_"+СчГрупп,Тип("ГруппаФормы"));
			ТекГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ТекГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			СчГрупп = СчГрупп + 1;
		КонецЕсли;
		ТекЭлемент = Элементы.Добавить("Маркер_"+Сч,Тип("ПолеФормы"),ТекГруппа);
		ТекЭлемент.Вид = ВидПоляФормы.ПолеКартинки;
		ТекЭлемент.КартинкаЗначений = БиблиотекаКартинок.МаркерыЦвета;
		ТекЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ТекЭлемент.Заголовок = XMLСтрока(Сч);
		ТекЭлемент.Ширина = 2;
		ТекЭлемент.Высота = 1;
		ТекЭлемент.РастягиватьПоГоризонтали = Ложь;
		ТекЭлемент.РастягиватьПоВертикали = Ложь;
		ТекЭлемент.ПутьКДанным = ТекЭлемент.Имя;
		ТекЭлемент.Рамка = Новый Рамка(?(Сч = 0, ТипРамкиЭлементаУправления.Одинарная, ТипРамкиЭлементаУправления.БезРамки), 1);
		ТекЭлемент.Гиперссылка = Истина;
		ТекЭлемент.УстановитьДействие("Нажатие", "НажатиеНаКартинку");
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура НажатиеНаКартинку(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Число(Элемент.Заголовок));
КонецПроцедуры


